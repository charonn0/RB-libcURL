#tag Class
Protected Class OptionInfo
	#tag Method, Flags = &h0
		Attributes( hidden = true )  Sub Constructor(Option As curl_easyoption)
		  mOpt = Option
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Shared Function GetOptionByName(Name As String) As Integer
		  Static Names As New Dictionary("ACCEPT_ENCODING":10102, "APPEND":50, "AUTOREFERER":58, "BUFFERSIZE":98, "CAINFO":10065, "CAPATH":10097, _
		  "CERTINFO":172, "CHUNK_BGN_FUNCTION":20198, "CHUNK_DATA":10201, "CHUNK_END_FUNCTION":20199, "CLOSESOCKETDATA":10209, "CLOSESOCKETFUNCTION":20208, _
		  "CONNECTTIMEOUT":78, "CONNECT_ONLY":141, "COOKIE":10022, "COOKIEFILE":10031, "COOKIEJAR":10082, "COOKIELIST":10135, "COOKIESESSION":96, _
		  "COPYPOSTFIELDS":10165, "CRLF":27, "CRLFILE":10169, "CURLU":10282, "CUSTOMREQUEST":10036, "DEBUGDATA":10095, "DEBUGFUNCTION":20094, _
		  "DEFAULT_PROTOCOL":10238, "DIRLISTONLY":48, "DISALLOW_USERNAME_IN_URL":278, "DNS_CACHE_TIMEOUT":92, "DNS_INTERFACE":10221, "DNS_LOCAL_IP4":10222, _
		  "DNS_LOCAL_IP6":10223, "DNS_SERVERS":10211, "DNS_SHUFFLE_ADDRESSES":275, "EGDSOCKET":10077, "ERRORBUFFER":10010, "EXPECT_100_TIMEOUT_MS":227, _
		  "FAILONERROR":45, "FILETIME":69, "FNMATCH_DATA":10202, "FNMATCH_FUNCTION":20200, "FOLLOWLOCATION":52, "FORBID_REUSE":75, "FRESH_CONNECT":74, _
		  "FTPPORT":10017, "FTP_ACCOUNT":10134, "FTP_ALTERNATIVE_TO_USER":10147, "FTP_CREATE_MISSING_DIRS":110, "FTP_FILEMETHOD":138, _
		  "HAPROXYPROTOCOL":274, "HEADER":42, "HEADERDATA":10029, "HEADERFUNCTION":20079, "HEADEROPT":229, "HTTP200ALIASES":10104, "HTTPAUTH":107, _
		  "HTTPGET":80, "HTTPHEADER":10023, "HTTPPOST":10024, "HTTPPROXYTUNNEL":61, "HTTPVERSION":84, "HTTP_CONTENT_DECODING":158, _
		  "HTTP_TRANSFER_DECODING":157, "IGNORE_CONTENT_LENGTH":136, "INFILESIZE":14, "IPRESOLVE":113, "ISSUERCERT":10170, "KEYPASSWD":10026, _
		  "KRBLEVEL":10063, "LOCALPORT":139, "LOCALPORTRANGE":140, "LOW_SPEED_LIMIT":19, "LOW_SPEED_TIME":20, "MAIL_FROM":10186, "MAIL_RCPT":10187, _
		  "MAXREDIRS":68, "MIMEPOST":10269, "NETINTERFACE":10062, "NETRC_FILE":10118, "NOBODY":44, "NOPROGRESS":43, "NOPROXY":10177, "OPENSOCKETDATA":10164, _
		  "OPENSOCKETFUNCTION":20163, "PASSWORD":10174, "PATH_AS_IS":234, "PIPELINING_SERVER_BL":10012, "PIPELINING_SITE_BL":10011, "PIPEWAIT":237, "PORT":3, _
		  "POSTFIELDS":10015, "POSTFIELDSIZE":60, "POSTQUOTE":10039, "POSTREDIR":161, "PREQUOTE":10093, "PRIVATE_":10103, "PROGRESSDATA":10057, _
		  "PROGRESSFUNCTION":20056, "PROTOCOLS":181, "PROXY":10004, "PROXYAUTH":111, "PROXYHEADER":10228, "PROXYPASSWORD":10176, "PROXYPORT":59, _
		  "PROXYTYPE":101, "PROXYUSERNAME":10175, "PROXYUSERPWD":10006, "PROXY_SERVICE_NAME":10235, "PUT":54, "QUOTE":10028, "RANDOM_FILE":10076, _
		  "RANGE":10007, "READDATA":10009, "READFUNCTION":20012, "REDIR_PROTOCOLS":182, "REFERER":10016, "RESOLVE":10203, "RESUME_FROM":21, _
		  "RESUME_FROM_LARGE":30116, "RTSP_SESSION_ID":10190, "RTSP_STREAM_URI":10191, "RTSP_TRANSPORT":10192, "SEEKDATA":10168, "SEEKFUNCTION":20167, _
		  "SHARE":10100, "SHOPT_LOCKFUNC":3, "SHOPT_UNLOCKFUNC":4, "SHOPT_USERDATA":5, "SOCKOPTDATA":10149, "SOCKOPTFUNCTION":20148, _
		  "SOCKS5_GSSAPI_SERVICE":10179, "SSH_AUTH_TYPES":151, "SSH_COMPRESSION":268, "SSH_HOST_PUBLIC_KEY_MD5":10162, "SSH_KNOWNHOSTS":10183, _
		  "SSH_PRIVATE_KEYFILE":10153, "SSH_PUBLIC_KEYFILE":10152, "SSLCERT":10025, "SSLCERTTYPE":10086, "SSLENGINE":10089, "SSLKEY":10087, _
		  "SSLKEYTYPE":10088, "SSLVERSION":32, "SSL_CIPHER_LIST":10083, "SSL_CTX_DATA":10109, "SSL_CTX_FUNCTION":20108, "SSL_ENABLE_ALPN":226, _
		  "SSL_ENABLE_NPN":225, "SSL_OPTIONS":216, "SSL_VERIFYHOST":81, "SSL_VERIFYPEER":64, "TIMEOUT":13, "UPKEEP_INTERVAL_MS":281, "UPLOAD":46, _
		  "UPLOAD_BUFFERSIZE":280, "URL":10002, "USERAGENT":10018, "USERNAME":10173, "USE_SSL":119, "USE_SSL_ALL":3, "VERBOSE":41, "WILDCARDMATCH":197, _
		  "WRITEDATA":10001, "WRITEFUNCTION":20011, "XFERINFODATA":10057, "XFERINFOFUNCTION":20219)
		  
		  Name = Replace(Name, "CURLOPT_", "")
		  Name = Replace(Name, "libcURL.Opts.", "")
		  Return Names.Lookup(Name, 0)
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Shared Function GetOptionName(OptionID As Integer, BindingAlias As Boolean) As String
		  Static Names As New Dictionary(10102:"ACCEPT_ENCODING", 50:"APPEND", 58:"AUTOREFERER", 98:"BUFFERSIZE", 10065:"CAINFO", 10097:"CAPATH", _
		  172:"CERTINFO", 20198:"CHUNK_BGN_FUNCTION", 10201:"CHUNK_DATA", 20199:"CHUNK_END_FUNCTION", 10209:"CLOSESOCKETDATA", 20208:"CLOSESOCKETFUNCTION", _
		  78:"CONNECTTIMEOUT", 141:"CONNECT_ONLY", 10022:"COOKIE", 10031:"COOKIEFILE", 10082:"COOKIEJAR", 10135:"COOKIELIST", 96:"COOKIESESSION", _
		  10165:"COPYPOSTFIELDS", 27:"CRLF", 10169:"CRLFILE", 10282:"CURLU", 10036:"CUSTOMREQUEST", 10095:"DEBUGDATA", 20094:"DEBUGFUNCTION", _
		  10238:"DEFAULT_PROTOCOL", 48:"DIRLISTONLY", 278:"DISALLOW_USERNAME_IN_URL", 92:"DNS_CACHE_TIMEOUT", 10221:"DNS_INTERFACE", 10222:"DNS_LOCAL_IP4", _
		  10223:"DNS_LOCAL_IP6", 10211:"DNS_SERVERS", 275:"DNS_SHUFFLE_ADDRESSES", 10077:"EGDSOCKET", 10010:"ERRORBUFFER", 227:"EXPECT_100_TIMEOUT_MS", _
		  45:"FAILONERROR", 69:"FILETIME", 10202:"FNMATCH_DATA", 20200:"FNMATCH_FUNCTION", 52:"FOLLOWLOCATION", 75:"FORBID_REUSE", 74:"FRESH_CONNECT", _
		  10017:"FTPPORT", 10134:"FTP_ACCOUNT", 10147:"FTP_ALTERNATIVE_TO_USER", 110:"FTP_CREATE_MISSING_DIRS", 138:"FTP_FILEMETHOD", 274:"HAPROXYPROTOCOL", _
		  42:"HEADER", 10029:"HEADERDATA", 20079:"HEADERFUNCTION", 229:"HEADEROPT", 10104:"HTTP200ALIASES", 107:"HTTPAUTH", 80:"HTTPGET", 10023:"HTTPHEADER", _
		  10024:"HTTPPOST", 61:"HTTPPROXYTUNNEL", 84:"HTTPVERSION", 158:"HTTP_CONTENT_DECODING", 157:"HTTP_TRANSFER_DECODING", 136:"IGNORE_CONTENT_LENGTH", _
		  14:"INFILESIZE", 113:"IPRESOLVE", 10170:"ISSUERCERT", 10026:"KEYPASSWD", 10063:"KRBLEVEL", 139:"LOCALPORT", 140:"LOCALPORTRANGE", _
		  19:"LOW_SPEED_LIMIT", 20:"LOW_SPEED_TIME", 10186:"MAIL_FROM", 10187:"MAIL_RCPT", 68:"MAXREDIRS", 10269:"MIMEPOST", 10062:"NETINTERFACE", _
		  10118:"NETRC_FILE", 44:"NOBODY", 43:"NOPROGRESS", 10177:"NOPROXY", 10164:"OPENSOCKETDATA", 20163:"OPENSOCKETFUNCTION", 10174:"PASSWORD", _
		  234:"PATH_AS_IS", 10012:"PIPELINING_SERVER_BL", 10011:"PIPELINING_SITE_BL", 237:"PIPEWAIT", 3:"PORT", 10015:"POSTFIELDS", 60:"POSTFIELDSIZE", _
		  10039:"POSTQUOTE", 161:"POSTREDIR", 10093:"PREQUOTE", 10103:"PRIVATE_", 10057:"PROGRESSDATA", 20056:"PROGRESSFUNCTION", 181:"PROTOCOLS", _
		  10004:"PROXY", 111:"PROXYAUTH", 10228:"PROXYHEADER", 10176:"PROXYPASSWORD", 59:"PROXYPORT", 101:"PROXYTYPE", 10175:"PROXYUSERNAME", _
		  10006:"PROXYUSERPWD", 10235:"PROXY_SERVICE_NAME", 54:"PUT", 10028:"QUOTE", 10076:"RANDOM_FILE", 10007:"RANGE", 10009:"READDATA", _
		  20012:"READFUNCTION", 182:"REDIR_PROTOCOLS", 10016:"REFERER", 10203:"RESOLVE", 21:"RESUME_FROM", 30116:"RESUME_FROM_LARGE", _
		  10190:"RTSP_SESSION_ID", 10191:"RTSP_STREAM_URI", 10192:"RTSP_TRANSPORT", 10168:"SEEKDATA", 20167:"SEEKFUNCTION", 10100:"SHARE", _
		  3:"SHOPT_LOCKFUNC", 4:"SHOPT_UNLOCKFUNC", 5:"SHOPT_USERDATA", 10149:"SOCKOPTDATA", 20148:"SOCKOPTFUNCTION", 10179:"SOCKS5_GSSAPI_SERVICE", _
		  151:"SSH_AUTH_TYPES", 268:"SSH_COMPRESSION", 10162:"SSH_HOST_PUBLIC_KEY_MD5", 10183:"SSH_KNOWNHOSTS", 10153:"SSH_PRIVATE_KEYFILE", _
		  10152:"SSH_PUBLIC_KEYFILE", 10025:"SSLCERT", 10086:"SSLCERTTYPE", 10089:"SSLENGINE", 10087:"SSLKEY", 10088:"SSLKEYTYPE", 32:"SSLVERSION", _
		  10083:"SSL_CIPHER_LIST", 10109:"SSL_CTX_DATA", 20108:"SSL_CTX_FUNCTION", 226:"SSL_ENABLE_ALPN", 225:"SSL_ENABLE_NPN", 216:"SSL_OPTIONS", _
		  81:"SSL_VERIFYHOST", 64:"SSL_VERIFYPEER", 13:"TIMEOUT", 281:"UPKEEP_INTERVAL_MS", 46:"UPLOAD", 280:"UPLOAD_BUFFERSIZE", 10002:"URL", _
		  10018:"USERAGENT", 10173:"USERNAME", 119:"USE_SSL", 3:"USE_SSL_ALL", 41:"VERBOSE", 197:"WILDCARDMATCH", 10001:"WRITEDATA", 20011:"WRITEFUNCTION", _
		  10057:"XFERINFODATA", 20219:"XFERINFOFUNCTION")
		  
		  Dim name As String = Names.Lookup(OptionID, "")
		  If Not BindingAlias And name <> "" Then name = "CURLOPT_" + name
		  If BindingAlias And name <> "" Then name = "libcURL.Opts." + name
		  
		  Return name
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Shared Function GetOptionType(OptionID As Integer) As curl_easytype
		  Select Case OptionID
		    ' numbers and bools
		  Case FTPPORT, FAILONERROR, PATH_AS_IS, PROXYPORT, HTTPPROXYTUNNEL, LOCALPORT, LOCALPORTRANGE, DNS_CACHE_TIMEOUT, BUFFERSIZE, PORT, DISALLOW_USERNAME_IN_URL, _
		    AUTOREFERER, FOLLOWLOCATION, MAXREDIRS, POSTREDIR, PUT, POSTFIELDSIZE, COOKIESESSION, HTTPGET, HTTPVERSION, IGNORE_CONTENT_LENGTH, HTTP_CONTENT_DECODING, _
		    HTTP_TRANSFER_DECODING, EXPECT_100_TIMEOUT_MS, PIPEWAIT, APPEND, FTP_CREATE_MISSING_DIRS, FTP_FILEMETHOD, CRLF, RESUME_FROM, FILETIME, DIRLISTONLY, _
		    NOBODY, INFILESIZE, UPLOAD, UPLOAD_BUFFERSIZE, TIMEOUT, LOW_SPEED_LIMIT, LOW_SPEED_TIME, FRESH_CONNECT, FORBID_REUSE, CONNECTTIMEOUT, IPRESOLVE, _
		    CONNECT_ONLY, USE_SSL, DNS_SHUFFLE_ADDRESSES, SSL_ENABLE_ALPN, SSL_ENABLE_NPN, SSLVERSION, SSL_VERIFYHOST, SSL_VERIFYPEER, CERTINFO, SSH_COMPRESSION, _
		    NOPROGRESS, VERBOSE, CURLU
		    Return curl_easytype.Number
		    
		  Case RESUME_FROM_LARGE
		    ' big numbers
		    Return curl_easytype.LargeNumber
		    
		  Case PROTOCOLS, REDIR_PROTOCOLS, PROXYTYPE, HTTPAUTH, PROXYAUTH, HEADEROPT, SSL_OPTIONS, SSH_AUTH_TYPES
		    ' numbers with defined meanings
		    Return curl_easytype.Bitmask
		    
		  Case WRITEFUNCTION, READFUNCTION, SEEKFUNCTION, SOCKOPTFUNCTION, OPENSOCKETFUNCTION, CLOSESOCKETFUNCTION, PROGRESSFUNCTION, XFERINFOFUNCTION, _
		    HEADERFUNCTION, DEBUGFUNCTION, SSL_CTX_FUNCTION, CHUNK_BGN_FUNCTION, CHUNK_END_FUNCTION, FNMATCH_FUNCTION
		    ' callback functions
		    Return curl_easytype.Subroutine
		    
		  Case WRITEDATA, READDATA, SEEKDATA, SOCKOPTDATA, OPENSOCKETDATA, CLOSESOCKETDATA, PROGRESSDATA, XFERINFODATA, HEADERDATA, DEBUGDATA, SSL_CTX_DATA, _
		    CHUNK_DATA, FNMATCH_DATA
		    ' callback function contexts
		    Return curl_easytype.Opaque
		    
		  Case ERRORBUFFER, HTTPPOST, MIMEPOST, RESOLVE, SSL_CIPHER_LIST, PRIVATE_, SHARE
		    ' void*
		    Return curl_easytype.Ptr
		    
		  Case HTTPHEADER, PROXYHEADER, HTTP200ALIASES, MAIL_RCPT, QUOTE, PREQUOTE, POSTQUOTE
		    ' ListPtr
		    Return curl_easytype.List
		    
		  Case URL, DEFAULT_PROTOCOL, PROXY, NOPROXY, SOCKS5_GSSAPI_SERVICE, NETINTERFACE, NETRC_FILE, PROXYUSERPWD, USERNAME, PASSWORD, PROXYUSERNAME, PROXYPASSWORD, _
		    ACCEPT_ENCODING, POSTFIELDS, COPYPOSTFIELDS, REFERER, USERAGENT, COOKIE, COOKIEFILE, COOKIEJAR, COOKIELIST, MAIL_FROM, FTP_ACCOUNT, RTSP_SESSION_ID, _
		    RTSP_STREAM_URI, RTSP_TRANSPORT, RANGE, CUSTOMREQUEST, DNS_INTERFACE, DNS_LOCAL_IP4, DNS_LOCAL_IP6, DNS_SERVERS, SSLCERT, SSLCERTTYPE, SSLKEY, _
		    SSLKEYTYPE, KEYPASSWD, SSLENGINE, CAINFO, ISSUERCERT, CAPATH, CRLFILE, RANDOM_FILE, EGDSOCKET, SSH_HOST_PUBLIC_KEY_MD5, SSH_PUBLIC_KEYFILE, _
		    SSH_PRIVATE_KEYFILE, SSH_KNOWNHOSTS
		    ' ptr to a string
		    Return curl_easytype.String
		    
		  Else
		    Return curl_easytype.Unknown
		  End Select
		End Function
	#tag EndMethod

	#tag Method, Flags = &h0
		Function Operator_Compare(OtherOpt As libcURL.Opts.OptionInfo) As Integer
		  Return Sign(OptionNumber - OtherOpt.OptionNumber)
		End Function
	#tag EndMethod

	#tag Method, Flags = &h0
		Function Operator_Convert() As Integer
		  Return OptionNumber
		End Function
	#tag EndMethod

	#tag Method, Flags = &h0
		Sub Operator_Convert(OptionID As Integer)
		  If Not libcURL.IsAvailable Then Raise New PlatformNotSupportedException
		  If libcURL.Version.IsAtLeast(7, 73, 0) Then
		    Dim opt As Ptr = curl_easy_option_by_id(OptionID)
		    If opt <> Nil Then Me.Constructor(opt.curl_easyoption)
		  Else
		    mOpt.Option = OptionID
		    mOpt.Type = GetOptionType(OptionID)
		  End If
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h0
		Sub Operator_Convert(Name As String)
		  If Not libcURL.IsAvailable Then Raise New PlatformNotSupportedException
		  If libcURL.Version.IsAtLeast(7, 73, 0) Then
		    Dim opt As Ptr = curl_easy_option_by_name(Name)
		    If opt <> Nil Then Me.Constructor(opt.curl_easyoption)
		  Else
		    mOpt.Option = GetOptionByName(Uppercase(Name))
		    mOpt.Type = GetOptionType(OptionNumber)
		  End If
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h0
		Function StringValue(Session As libcURL.EasyHandle) As String
		  Select Case Me.Type
		  Case curl_easytype.Bitmask, curl_easytype.LargeNumber, curl_easytype.Number
		    #If Target64Bit Then
		      Return Str(Me.Value(Session).Int64Value)
		    #Else
		      Return Str(Me.Value(Session).Int32Value)
		    #EndIf
		    
		  Case curl_easytype.List, curl_easytype.Opaque, curl_easytype.Ptr
		    Return "0x" + Hex(Me.Value(Session).PtrValue)
		    
		  Case curl_easytype.Subroutine
		    Return "[delegate]"
		    
		  Else
		    Return ""
		  End Select
		End Function
	#tag EndMethod

	#tag Method, Flags = &h0
		Function Value(Session As libcURL.EasyHandle) As Variant
		  Return Session.GetOption(OptionNumber)
		End Function
	#tag EndMethod

	#tag Method, Flags = &h0
		Sub Value(Session As libcURL.EasyHandle, Assigns NewValue As Variant)
		  Call Session.SetOption(OptionNumber, NewValue)
		End Sub
	#tag EndMethod


	#tag ComputedProperty, Flags = &h0
		#tag Getter
			Get
			  return GetOptionName(OptionNumber, True)
			End Get
		#tag EndGetter
		BindingAlias As String
	#tag EndComputedProperty

	#tag ComputedProperty, Flags = &h0
		#tag Getter
			Get
			  If Name <> "" Then Return "https://curl.haxx.se/libcurl/c/" + Name + ".html"
			End Get
		#tag EndGetter
		DocumentationURL As String
	#tag EndComputedProperty

	#tag Property, Flags = &h21
		Private mOpt As curl_easyoption
	#tag EndProperty

	#tag ComputedProperty, Flags = &h0
		#tag Getter
			Get
			  Dim mb As MemoryBlock = mOpt.Name
			  If mb <> Nil Then
			    Return mb.CString(0)
			  Else
			    Return GetOptionName(OptionNumber, False)
			  End If
			End Get
		#tag EndGetter
		Name As String
	#tag EndComputedProperty

	#tag ComputedProperty, Flags = &h0
		#tag Getter
			Get
			  Return mOpt.Option
			End Get
		#tag EndGetter
		OptionNumber As Integer
	#tag EndComputedProperty

	#tag ComputedProperty, Flags = &h0
		#tag Getter
			Get
			  Return mOpt.Type
			End Get
		#tag EndGetter
		Type As libcURL.Opts.curl_easytype
	#tag EndComputedProperty


End Class
#tag EndClass
